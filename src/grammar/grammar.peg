/*
 * The @font-face src grammar used. https://www.w3.org/TR/css-fonts-4/#font-face-src-parsing
 *
 * Update the generated parser by running
 *   $ npm run build:grammar
 */

start
  = sourceEntries
  / '' { return [] }

sourceEntries
  = x:sourceEntry whitespace* "," whitespace* xs:sourceEntries { return [x].concat(xs); }
  / entry:sourceEntry { return [entry]; }

sourceEntry
  = urlEntry
  / localEntry

urlEntry = url:url format:format? tech:tech? { 
  return { 
    url: url, 
    format: format == null ? undefined : format,
    tech: tech == null ? undefined : tech,
  };
}

url
  = "url(" value:value ")" { return value; }

format
  = whitespace* "format(" value:value ")" { return value; }

tech 
  = whitespace* "tech(" keyword:[a-z0-9A-Z-]+ ")" { return keyword.join(""); }

localEntry
  = "local(" value:value ")" { return {local: value}; }

value
  = chars:[^)]+ { return util.extractValue(chars.join("")); }

whitespace
  = [ \t\r\n\f]
